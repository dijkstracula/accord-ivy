#lang ivy1.8

# Some functionality for logical timestamp operations.

include collections
include order
include types

global {
    instance walltime : unbounded_sequence
    instance sequence : unbounded_sequence

    class timestamp = {
        # TODO: epoch???
        field real : walltime
        field seqid: sequence
        field host : node
    }

    function (LHS: timestamp < RHS: timestamp) =
        LHS.real < RHS.real |
        LHS.real = RHS.real & LHS.seqid < RHS.seqid | 
        LHS.real = RHS.real & LHS.seqid = RHS.seqid & LHS.host < RHS.host

    function (LHS: timestamp = RHS: timestamp) =
        LHS.real = RHS.real & LHS.seqid = RHS.seqid & LHS.host = RHS.host

}

module time(self: node) = {

    implementation {
        var curr_time   : walltime
        var last_issued : timestamp

        after init {
            curr_time := 1;
            last_issued.real := 0;
            last_issued.seqid := 0;
            last_issued.host := self;

            prev_issued := last_issued; #ghost
        }

        # Advance time on occasion.
        action tick
        implement tick {
            curr_time := curr_time.next;
        }

        # Get "the current time".
        #export action now returns (ret : timestamp)
        # TODO: should we only return our "monotonic local clock"?
        action now returns (ret : timestamp)
        implement now {
            ret.host := self;
           
            tick();

            ret.real := curr_time;
            ret.seqid := 0;

            prev_issued := last_issued; #ghost
            last_issued := ret;
        }

        #TODO: How to put an upper bound?
        #invariant [ts_lt_nonreflexivity] ts_lt(TS,TS) = false
    } 

    specification {
        var prev_issued : timestamp
        after now {
            # Our clock must always move forward, no matter how coarse the real
            # clock is.
            ensure prev_issued < last_issued;
        }
    }
}

#axiom host(0).sock.id ~= host(1).sock.id
